buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true

        // https://github.com/SpongePowered/MixinGradle
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
allprojects {
    apply plugin: 'net.minecraftforge.gradle'
    apply plugin: 'eclipse'
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'

    dependencies {
        minecraft "net.minecraftforge:forge:${minecraftVersion}-${forgeVersion}"
    }
}
apply plugin: 'org.spongepowered.mixin'

version = "${minecraftVersion}-${modVersion}"
group = "io.${groupName}"
archivesBaseName = project.modId

// Need this here so eclipse task generates correctly.
compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility

repositories {
    maven { url 'https://repo.spongepowered.org/maven' }
}

mixin {
    add sourceSets.main, "mixins.${modId}.refmap.json"
}

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: "${mappingBuild}-${mappingMCVersion}"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=" + archivesBaseName + ".mixins.json"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'

            mods { timelib { source sourceSets.main } }
        }
        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'

            mods { timelib { source sourceSets.main } }
        }
    }
}

dependencies {
    // Project API
    api project("${rootProject.name}-api")

    // https://github.com/SpongePowered/Mixin
    compile "org.spongepowered:mixin:0.8.1-SNAPSHOT"
}

final def jarAttributes = [
        "Specification-Title": project.modId,
        "Specification-Vendor": "${groupName}",
        "Specification-Version": "1", // We are version 1 of ourselves
        "Implementation-Title": project.name,
        "Implementation-Version": "${version}",
        "Implementation-Vendor": "${groupName}",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        "MixinConfigs": "${modId}.mixins.json"
]

// Get properties into the manifest for reading by the runtime
jar {
    // Include API main sourceSet in jar
    from files(project("${rootProject.name}-api").sourceSets.main.output)
    manifest { attributes(jarAttributes) }
}
// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')

task apiJar(type: Jar, dependsOn: classes) {
    classifier = 'api'
    from files(project("${rootProject.name}-api").sourceSets.main.output)
}
apiJar.finalizedBy('reobfJar')
build.dependsOn apiJar

task devJar(type: Jar, dependsOn: classes) {
    classifier = 'dev'
    from sourceSets.main.output
    // Include API main sourceSet in jar
    from files(project("${rootProject.name}-api").sourceSets.main.output)
    exclude("mixins.${modId}.refmap.json")
    manifest { attributes(jarAttributes) }
}

// Generate sources when building mod
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
build.dependsOn sourcesJar

artifacts {
    archives sourcesJar, devJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
        mavenJava(MavenPublication) {
            artifact apiJar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

// Process resources on build and make sure that variables are
// correctly inserted into mods.toml when the mod is built or run
processResources {
    // This will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version

    // Replace stuff in mods.toml, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        // Replace version
        expand 'version': project.version
    }
    // Copy everything else except the mods.toml
    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}
