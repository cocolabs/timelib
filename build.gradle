buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true

        // https://github.com/SpongePowered/MixinGradle
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java-library'

apply plugin: 'org.spongepowered.mixin'

version = "${minecraftVersion}-${modVersion}"
group = "io.${groupName}"
archivesBaseName = project.modId

// Need this here so eclipse task generates correctly.
compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility

repositories {
    maven { url 'https://repo.spongepowered.org/maven' }
}

sourceSets {
    api {
        java { srcDir('src/api/java') }
    }
    main {
        java {
            compileClasspath += api.output
            runtimeClasspath += api.output
        }
    }
}

// Define which sourceSets will be covered by mixin refmap
mixin {
    add sourceSets.main, "mixins.${modId}.refmap.json"
}

minecraft {
    // The mappings can be changed at any time, use non-default mappings at your own risk.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: "${mappingBuild}-${mappingMCVersion}"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=" + archivesBaseName + ".mixins.json"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'

            mods { timelib { source sourceSets.main } }
        }
        server {
            workingDirectory project.file('run')
            arg "-mixin.config=" + archivesBaseName + ".mixins.json"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'

            mods { timelib { source sourceSets.main } }
        }
    }
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:${minecraftVersion}-${forgeVersion}"

    // https://github.com/SpongePowered/Mixin
    compile "org.spongepowered:mixin:0.8.1-SNAPSHOT"

    implementation sourceSets.api.output
}

final def jarAttributes = [
        "Specification-Title": project.modId,
        "Specification-Vendor": "${groupName}",
        "Specification-Version": "1", // We are version 1 of ourselves
        "Implementation-Title": project.name,
        "Implementation-Version": "${version}",
        "Implementation-Vendor": "${groupName}",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        "MixinConfigs": "${modId}.mixins.json"
]

jar {
    from sourceSets.api.output
    from sourceSets.main.output
    manifest { attributes(jarAttributes) }
}
// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')

// API jar for library consumers
task apiJar(type: Jar, dependsOn: classes) {
    baseName = 'timelib'
    classifier = 'api'
    from sourceSets.api.output
}
apiJar.finalizedBy('reobfJar')

// Non-obfuscated jar for developers
task devJar(type: Jar, dependsOn: classes) {
    baseName = 'timelib'
    classifier = 'dev'
    from sourceSets.api.output
    from sourceSets.main.output
    exclude("mixins.${modId}.refmap.json")
    manifest { attributes(jarAttributes) }
}

// Generate sources when building mod
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
build.dependsOn sourcesJar

artifacts {
    archives sourcesJar, devJar, apiJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
        mavenJava(MavenPublication) {
            artifact apiJar
        }
        mavenJava(MavenPublication) {
            artifact devJar
        }
        mavenJava(MavenPublication) {
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

// this step is necessary when installing both jars in the local maven repository
//install {
//    repositories.mavenInstaller {
//        addFilter('api') { artifact, file -> artifact.name.endsWith('api') }
//        addFilter('dev') { artifact, file -> artifact.name.endsWith('dev') }
//    }
//}

// Process resources on build and make sure that variables are
// correctly inserted into mods.toml when the mod is built or run
processResources {
    // This will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version

    // Replace stuff in mods.toml, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        // Replace version
        expand 'version': project.version
    }
    // Copy everything else except the mods.toml
    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}
